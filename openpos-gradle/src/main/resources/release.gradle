

def gitCommit = { file ->
    println('add')
    exec {
        commandLine 'git', 'add', file
    }
    println('config')
    exec {
        commandLine 'git', 'config', 'user.email', 'build@jumpmind.com'
    }
    exec {
        commandLine 'git', 'config', 'user.name', 'build'
    }
    exec {
        commandLine 'git', 'config', 'remote.origin.url', remoteRepoUrl.replace('${personalAccessToken}', personalAccessToken)
    }

    println('commit')
    exec {
        commandLine 'git', 'commit', '-m', "releasing ${version}"
    }
    println('push')
    exec {
        commandLine 'git', 'push', 'origin', 'HEAD:'+ gitBranch
    }
}

task prerelease() {

    doLast {
        if (version.endsWith('-SNAPSHOT')) {
            version = version.substring(0, version.length()-'-SNAPSHOT'.length())
        }

        def propertyFile = file "gradle.properties"
        def props = new Properties()
        propertyFile.withInputStream { props.load(it) }
        props['version'] = version
        propertyFile.withWriter { props.store(it, null) }
        gitCommit propertyFile.name
        println "prerelease ${version}"
    }
}
