apply plugin: "com.moowork.node"

repositories {
    mavenCentral()
}

configurations {
    sshAntTask
}
dependencies {
    sshAntTask 'org.apache.ant:ant-jsch:1.10.5'
    sshAntTask 'com.jcraft:jsch:0.1.55'
}

node {
    version = '10.5.0'
    npmVersion = '6.1.0'
    download = downloadNode.toBoolean()
    println """
      **************************************************************************************************
        Using native node & npm? ${!download}.  Set downloadNode to true in gradle.properties 
        if you want to use downloaded versions of node and npm
      **************************************************************************************************
      """
    workDir = file("${projectDir}/node")
    nodeModulesDir = file("./")
}

task npm_install(type: NpmTask){
    args = [
            'install'
    ]
}

task ng_test(type: NpmTask){
    dependsOn npm_install
    args = [
            'run', 'ng', 'test', 'openpos-client-core-lib'
    ]
}

task ng_build_angular(type: NpmTask){
    dependsOn npm_install
    args = [
            'run',
            'ng', 'build', 'openpos-client-core-lib'
    ]
}

task bundle_sass(type: NpmTask){
    dependsOn ng_build_angular
    args = [
            'run',
            'scss-bundle'
    ]
}

task processResources {}

task test {
    dependsOn ng_test
}

task build {
    dependsOn bundle_sass
}

task npm_pack(type: NpmTask){
    dependsOn build
    workingDir =  file('dist/openpos-client-core-lib')
    args = [
            'pack'
    ]
}

task deploy {
    dependsOn npm_pack
    doLast {
        if (!gitBranch.equals('merge')) {
            ant.taskdef(name: 'scp', classname: 'org.apache.tools.ant.taskdefs.optional.ssh.Scp', classpath: configurations.sshAntTask.asPath)
            def toDir = "$deployUser:$deployPassword@maven.jumpmind.com:~/www/npm"

            fileTree('dist/openpos-client-core-lib').visit { FileVisitDetails details -> 
                if (details.file.path.endsWith('tgz')) {
                    ant.scp(todir: toDir, port: 2222, trust: 'true', verbose: 'true', localFile: details.file.path)          
                    def snapshotFile = 'dist/openpos-client-core-lib/jumpmind-openpos-client-core-lib-snapshot.tgz'
                    ant.copy(file: details.file.path, tofile: snapshotFile)
                    ant.scp(todir: toDir, port: 2222, trust: 'true', verbose: 'true', localFile: snapshotFile)
                }
            }
        } else {
            println('Not going to deploy this build because the gitBranch indicates that this is a pull request')
        }
    }
}